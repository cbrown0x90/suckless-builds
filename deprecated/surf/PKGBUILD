# $Id$
# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Pierre Chapuis <catwell at archlinux dot us>
# Contributor: Ray Kohler <ataraxia937 at gmail dot com>
# Contributor: Michael Witten <mfwitten>
# Contributor: Raphael Proust <raphlalou at gmail dot com>

pkgname=surf
pkgver=0.6
pkgrel=2
pkgdesc='A simple web browser based on WebKit/GTK+.'
arch=('i686' 'x86_64')
url='http://surf.suckless.org/'
license=('MIT')
depends=('webkitgtk2' 'xorg-xprop')
optdepends=('dmenu: URL-bar'
            'ca-certificates: SSL verification'
            'xterm: default download handler'
            'curl: default download handler')
makedepends=('pkgconfig')
source=("http://dl.suckless.org/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        'custom-patch.diff')
md5sums=('aeeed723b562a30cc6a2b3ea18f6d99a'
        '814d28ed84f7e7d01bc5d8dd8237bbc4')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    
    patch -p1 < ../custom-patch.diff
                                                 
    sed -i 's/CPPFLAGS =/CPPFLAGS +=/g' config.mk
    sed -i 's/CFLAGS =/CFLAGS +=/g' config.mk
    sed -i 's/LDFLAGS =/LDFLAGS +=/g' config.mk
                                           
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
                                       
    make PREFIX=/usr DESTDIR="${pkgdir}" install
    install -Dm0644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
